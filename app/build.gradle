apply plugin: 'com.android.application'
apply plugin: 'org.greenrobot.greendao'

android {
    compileSdkVersion 27
    defaultConfig {
        applicationId "com.achpay.wallet"
        minSdkVersion 16
        targetSdkVersion 27
        versionCode 4
        versionName "1.0.5"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        multiDexEnabled true

        externalNativeBuild {
            cmake {
                cppFlags ""
                //生成多个版本的so文件
                abiFilters 'armeabi', 'arm64-v8a', 'armeabi-v7a', 'x86', 'x86_64'
            }
        }
    }

    flavorDimensions "channel"
    productFlavors {
        DEFAULT { dimension "channel" }
        GOOGLE_PLAY { dimension "channel" }
        HAO_JIN { dimension "channel" }
        POS { dimension "channel" }
    }
    productFlavors.all { flavor ->
        flavor.manifestPlaceholders = [CHANNEL_NAME: name]
    }

    // 自定义apk输出路径和文件名
    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            if (variant.buildType.name.equals('release')) {
                // 自定义输出路径
                variant.getPackageApplication().outputDirectory = new File("/Users/dawnton/Downloads/APK")
                // 自定义文件名{示例：AppName-Flavor-debug-v1.0.0_201807301409}
                outputFileName = "Alchemy-${variant.flavorName}-${variant.buildType.name}-v${variant.versionName}.apk"
            } else {
                def outputFile = output.outputFile
                if (outputFile != null && outputFile.name.endsWith('.apk')) {
                    copy {
                        from outputFile
                        into "/Users/dawnton/Downloads/APK"
                        rename {
                            "Alchemy-debug-v${variant.versionName}.apk"
                        }
                    }
                }
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        disable 'GoogleAppIndexingWarning'
        checkReleaseBuilds false
        abortOnError false
    }

    //配置CMakeLists.txt路径
    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"  // 设置所要编写的c源码位置，以及编译后so文件的名字
        }
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
        }
    }
}

greendao {
    //指定数据库schema版本号，迁移等操作会用到
    schemaVersion 4
    //dao的包名，包名默认是entity所在的包
    daoPackage 'com.achpay.wallet.database'
    //生成数据库文件的目录，会生成DaoMaster,DaoSession,XXXDao都放在此目录下
    targetGenDir 'src/main/java'
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support.constraint:constraint-layout:1.1.2'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'

    implementation 'com.android.support:design:27.1.1'
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support:recyclerview-v7:27.1.1'
    implementation 'com.scwang.smartrefresh:SmartRefreshLayout:1.0.5.1'
    implementation 'com.nineoldandroids:library:2.4.0'
    implementation 'com.google.zxing:core:3.3.3'
    implementation 'com.squareup.okhttp3:okhttp:3.10.0'
    implementation 'com.squareup.retrofit2:retrofit:2.4.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.4.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.4.0'
    implementation 'io.reactivex.rxjava2:rxjava:2.1.14-RC1'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'
    implementation 'io.reactivex:rxandroid:1.2.1'
    implementation 'com.google.code.gson:gson:2.8.4'
    implementation 'com.github.bumptech.glide:glide:4.7.1'
    implementation 'com.neovisionaries:nv-websocket-client:2.5'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.7.1'
    implementation 'org.greenrobot:greendao:3.2.2'
    implementation 'com.yanzhenjie:permission:2.0.0-rc12'
    implementation 'org.web3j:core:3.3.1-android'
    implementation 'com.android.support:multidex:1.0.3'
    implementation 'com.crazysunj:cardslideview:1.3.1'
//    implementation 'cn.bingoogolapple:bga-qrcode-zbar:1.2.5'
    implementation 'cn.bingoogolapple:bga-qrcode-zxing:1.3.6'
    implementation 'me.devilsen:CZXing:0.9.5'

    implementation project(':progresshub')
}
